PREFIX dataact: <http://www.semanticweb.org/dataact#>
PREFIX dpv: <https://w3c.github.io/dpv/2.2/dpv/#>

SELECT DISTINCT 
    ?b2bSharing 
    ?holder 
    ?recipient 
    ?data
    ("UNJUSTIFIED_REFUSAL" AS ?violationType)
    ("Article 8(6) VIOLATED: Data holder refused to share data without trade secret justification" AS ?details)
WHERE {
  ?b2bSharing a dataact:B2BDataSharing ;
              dataact:governedBy ?contract .
  ?contract dpv:hasRecipient ?recipient .
  ?recipient a dataact:DataRecipient .
  ?b2bSharing dataact:authorizedBy ?user .
  ?user a dataact:User .
  ?holder a dataact:DataHolder ;
          dpv:hasData ?data .
  ?data a dataact:ProductData .
  
  FILTER NOT EXISTS {
    ?holder dataact:performsLegalAction ?provision .
    ?provision a dataact:DataProvision .
  }
  
  FILTER NOT EXISTS {
    ?data dataact:containsTradeSecret ?secret .
    ?secret a dataact:TradeSecretData .
  }
}
ORDER BY ?recipient



