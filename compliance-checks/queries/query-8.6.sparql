PREFIX dataact: <http://www.semanticweb.org/dataact#>
PREFIX dpv: <https://w3c.github.io/dpv/2.2/dpv/#>

SELECT DISTINCT 
    ?b2bSharing 
    ?holder 
    ?recipient 
    ?data
    ("UNJUSTIFIED_REFUSAL" AS ?violationType)
    ("Article 8(6) VIOLATED: Data holder refused to share data without trade secret justification" AS ?details)
WHERE {
  # B2B sharing agreement with authorization
  ?b2bSharing a dataact:B2BDataSharing ;
              dataact:governedBy ?contract ;
              dataact:authorizedBy ?user .
  
  # User owns/uses a product
  ?user a dataact:User ;
        dataact:ownsOrUses ?product .
  
  # Product generates data
  ?product a dataact:ConnectedProduct ;
           dataact:generatesData ?data .
  
  # Data holder has this data
  ?holder a dataact:DataHolder ;
          dpv:hasData ?data .
  
  # Contract has recipient
  ?contract dpv:hasRecipient ?recipient .
  ?recipient a dataact:DataRecipient .
  
  # VIOLATION: No data provision action exists
  FILTER NOT EXISTS {
    ?holder dataact:performsLegalAction ?provision .
    ?provision a dataact:DataProvision .
  }
  
  # No trade secret justification exists
  FILTER NOT EXISTS {
    ?data dataact:containsTradeSecret ?secret .
    ?secret a dataact:TradeSecretData .
  }
}
ORDER BY ?recipient




#### OUTPUTS:

# b2bSharing || holder || recipient data || violationType || details
# http://www.semanticweb.org/dataact#agreement247 || http://www.semanticweb.org/dataact#RobotManufacturer || http://www.semanticweb.org/dataact#autoRepair || http://www.semanticweb.org/dataact#RobotData || "UNJUSTIFIED_REFUSAL" || "Article 8(6) VIOLATED: Data holder refused to share data without trade secret justification"